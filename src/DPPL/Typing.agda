open import Lib.Algebra.Reals

module DPPL.Typing (R : Realsâ‚€) where

open import DPPL.Syntax R
open import DPPL.Regularity

open import Lib.Prelude
open import Lib.LocallyNameless.Unfinite
open import Lib.LocallyNameless.AbstractionConcretion
open import Lib.LocallyNameless.BindingSignature
open import Lib.Data.Vector
open import Lib.Syntax.Env

open import Order.Base
open import Order.Lattice

open VecSyntax
open VectorSyntax using () renaming (_âˆ·_ to _âˆ·áµ›_)
open Regâ†“â‰¤ renaming (_â‰¤_ to _â‰¤reg_)
open Effâ‰¤  renaming (_â‰¤_ to _â‰¤eff_)
open is-lattice Regâ†“-lattice

TyEnv : Type
TyEnv = Env Ty

module TypingVars where
  variable
    Î“ Î“' : TyEnv
    a    : ğ”¸

open SyntaxVars
open TypingVars

PrimTy : (Ï• : Prim) â†’ Coeff ^ PrimAr Ï• Ã— Coeff
PrimTy padd    = make Aâ†“ , Aâ†“
PrimTy pmul    = make Aâ†“ , Aâ†“
PrimTy psin    = make Aâ†“ , Aâ†“
PrimTy pnormal = make Mâ†“ , Mâ†“
PrimTy pgamma  = make Mâ†“ , Mâ†“
PrimTy pwiener = lookup (Mâ†“ âˆ· Câ†“ âˆ· []) , Câ†“

infix 5 _<:_
data _<:_ : Ty â†’ Ty â†’ Type where

  sreal :
    (_ : c â‰¤reg c')
    â†’ -----------------
    treal c <: treal c'

  stup :
    {Ts Ts' : Ty ^ n}
    (_ : âˆ€ i â†’ Ts i <: Ts' i)
    â†’ -----------------------
    ttup n Ts <: ttup n Ts'

  sarr :
    {Tâ‚ Tâ‚' Tâ‚‚ Tâ‚‚' : Ty}
    (_ : Tâ‚' <: Tâ‚)
    (_ : Tâ‚‚ <: Tâ‚‚')
    (_ : c â‰¤reg c')
    (_ : e â‰¤eff e')
    â†’ --------------------------------------
    Tâ‚ â‡’[ c , e ] Tâ‚‚ <: Tâ‚' â‡’[ c' , e' ] Tâ‚‚'

  sdist :
    (_ : T <: T')
    â†’ -----------------
    tdist T <: tdist T'


data _<:áµ‰_ : TyEnv â†’ TyEnv â†’ Type where
  snil  : Îµ <:áµ‰ Îµ
  scons
    : {Hâˆ‰' : a âˆ‰ dom Î“'} {Hâˆ‰ : a âˆ‰ dom Î“}
    â†’ T' <: T â†’ Î“' <:áµ‰ Î“ â†’ (Î“' â–¸ (a , T') [ Hâˆ‰' ]) <:áµ‰ (Î“ â–¸ (a , T) [ Hâˆ‰ ])

_â‰¤áµ‰_ : TyEnv â†’ Coeff â†’ Type
Î“ â‰¤áµ‰ c = âˆ€ {a T} â†’ a âˆ¶ T âˆˆ Î“ â†’ T â‰¤áµ— c


infix 4 _âŠ¢_:[_]_
data _âŠ¢_:[_]_ : TyEnv â†’ Tm â†’ Eff â†’ Ty â†’ Type where

  tsub :
    (_ : Î“ âŠ¢ t :[ e ] T)
    (_ : e â‰¤eff e')
    (_ : T <: T')
    â†’ ------------------
    Î“ âŠ¢ t :[ e' ] T'

  tpromote :
    (_ : Î“ âŠ¢ t :[ e ] T)
    (_ : Î“ â‰¤áµ‰ c)
    (_ : Î“ âŠ† Î“')
    â†’ ------------------
    Î“' âŠ¢ t :[ e ] c âˆ©áµ— T

  tvar :
    (_ : a âˆ¶ T âˆˆ Î“)
    â†’ -------------------
    Î“ âŠ¢ fvar a :[ det ] T

  tlam :
    {t : Tm ^ 1}
    (_ : Ğ˜[ a âˆˆ ğ”¸ ] (Î“ , a âˆ¶ T) âŠ¢ conc (t â‚€) a :[ e ] T')
    â†’ ---------------------------------------------------
    Î“ âŠ¢ lam T â–¸ t :[ det ] T â‡’[ Aâ†“ , e ] T'

  tapp :
    {ts : Tm ^ 2}
    (_ : Î“ âŠ¢ ts â‚€ :[ e ] T â‡’[ Aâ†“ , e ] T')
    (_ : Î“ âŠ¢ ts â‚ :[ e ] T)
    â†’ ------------------------------------
    Î“ âŠ¢ app â–¸ ts :[ e ] T'

  tprim :
    {cs : Coeff ^ PrimAr Ï•}
    {t : Tm ^ 1}
    (_ : PrimTy Ï• â‰¡ (cs , c))
    (_ : Î“ âŠ¢ t â‚€ :[ e ] treals _ cs)
    â†’ ------------------------------
    Î“ âŠ¢ prim Ï• â–¸ t :[ e ] treal c

  treal :
    {t : Tm ^ 0}
    â†’ -------------------------------
    Î“ âŠ¢ oreal r â–¸ t :[ det ] treal Aâ†“

  ttup :
    {Ts : Ty ^ n}
    {ts : Tm ^ n}
    (_ : âˆ€ i â†’ Î“ âŠ¢ ts i :[ e ] Ts i)
    â†’ ------------------------------
    Î“ âŠ¢ tup n â–¸ ts :[ e ] ttup n Ts

  tproj :
    {Ts : Ty ^ n}
    {t : Tm ^ 1}
    (i : Fin n)
    (_ : Î“ âŠ¢ t â‚€ :[ e ] ttup n Ts)
    â†’ --------------------------------
    Î“ âŠ¢ proj n i â–¸ t :[ e ] Ts i

  tif :
    {cs : Coeff ^ n}
    {ts : Tm ^ 3}
    (_ : Î“ âŠ¢ ts â‚€ :[ e ] treal Pâ†“)
    (_ : Î“ âŠ¢ ts â‚ :[ e ] treals n cs)
    (_ : Î“ âŠ¢ ts â‚‚ :[ e ] treals n cs)
    (_ : âˆ€ i â†’ Pâ†“ â‰¤reg cs i)
    â†’ -------------------------------
    Î“ âŠ¢ if â–¸ ts :[ e ] treals n cs

  tuniform :
    {t : Tm ^ 0}
    â†’ --------------------------------
    Î“ âŠ¢ ouniform â–¸ t :[ rnd ] treal Mâ†“

  tsample :
    {t : Tm ^ 1}
    (_ : Î“ âŠ¢ t â‚€ :[ rnd ] tdist T)
    â†’ ----------------------------
    Î“ âŠ¢ sample â–¸ t :[ rnd ] T

  tweight :
    {t : Tm ^ 1}
    (_ : Î“ âŠ¢ t â‚€ :[ rnd ] treal Mâ†“)
    â†’ -----------------------------
    Î“ âŠ¢ weight â–¸ t :[ rnd ] tunit

  tinfer :
    {t : Tm ^ 1}
    (_ : Î“ âŠ¢ t â‚€ :[ e ] tunit â‡’[ Mâ†“ , rnd ] T)
    â†’ ----------------------------------------
    Î“ âŠ¢ infer â–¸ t :[ e ] tdist T

  tdiff :
    {ts : Tm ^ 3}
    (_ : Î“ âŠ¢ ts â‚€ :[ e ] treals m (make c) â‡’[ Pâ†“ , det ] treals n (make c))
    (_ : Î“ âŠ¢ ts â‚ :[ e ] treals m (make c))
    (_ : Î“ âŠ¢ ts â‚‚ :[ e ] treals m (make Aâ†“))
    (_ : c â‰¡ Aâ†“ âŠ c â‰¡ Pâ†“)
    â†’ ---------------------------------------------------------------------
    Î“ âŠ¢ diff â–¸ ts :[ e ] treals n (make Aâ†“)

  tsolve :
    {ts : Tm ^ 3}
    (_ : Î“ âŠ¢ ts â‚€ :[ e ] treals (1 + n) (c âˆ·áµ› make Aâ†“) â‡’[ Câ†“ , det ] treals n (make Aâ†“))
    (_ : Î“ âŠ¢ ts â‚ :[ e ] treals (1 + n) (c âˆ·áµ› make Aâ†“))
    (_ : Î“ âŠ¢ ts â‚‚ :[ e ] treal (c âˆ© PCâ†“))
    (_ : c â‰¡ Aâ†“ âŠ c â‰¡ Câ†“)
    â†’ ----------------------------------------------------------------------------------
    Î“ âŠ¢ solve â–¸ ts :[ e ] treals (1 + n) (make Aâ†“)
